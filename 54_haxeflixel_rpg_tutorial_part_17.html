<html><head><style>body{font-family:Arial, Helvetica, sans-serif; padding:20px;}pre{ background:#eee; padding:20px; }footer{color:#666; border-top:1px solid #666; margin-top:20px; padding-top:20px; width:100%;}</style><title>54. HaxeFlixel RPG tutorial: Part 17</title></head><body><h1>54. HaxeFlixel RPG tutorial: Part 17</h1><h3>2014-10-25</h3><img alt="HaxeFlixel RPG tutorial" src="img/113.png" class="thumb">
<p>Today we'll begin adding the saving functionality.</p>
<p>HaxeFlixel provides a FlxSave class, which can be used to easily save and load data locally.</p>
<p>We already have a "Load game" button in the main menu, it's also needed to add a "Save" button to the game. We'll only show it when the player is outside of combat.</p>
<p>The game data will be saved under a specific keyword, such as "RPG_Save", which we can store as a static variable of our PlayState class.</p>
<p>With all of that in mind, introduce these three variables in the PlayState.hx class:</p><pre><code class="haxe">private var saveButton:FlxButton;
public static var SAVE_NAME:String = "RPG_Save";
public var loadedGame:Bool;
</code></pre>
<p>The first two I already explained, the third variable will simply determine whether the game needs to load any data after its instantiated.</p>
<p>Add this to the bottom of the create() function:</p>
<pre><code class="haxe">saveButton = new FlxButton(FlxG.width - 80, 0, &quot;Save&quot;, doSave);
add(saveButton);

if (loadedGame) {
	loadGame();
}
</code></pre>
<p>Here we create a button with a doSave() click handler, and check if the game was loaded (this value is set elsewhere), calling the loadGame() method if it was.</p>
<p>We only want to show the save button when the player is in Walking mode, outside of combat. Add this line to the update() function to manage its visibility:</p>
<pre><code class="haxe">// Save visibility
saveButton.visible = currentAction == Walking;
</code></pre>
<p>Now for the doSave() function. Here's the code, which I will explain shortly:</p>
<pre><code class="haxe">private function doSave():Void {
	var save:FlxSave = new FlxSave();
	// Delete all existing data
	save.bind(SAVE_NAME);
	save.erase();
	// Write new data
	save.bind(SAVE_NAME);
	save.data.hp = hud.hp;
	save.data.maxHp = hud.maxHp;
	save.data.exp = hud.exp;
	save.data.maxExp = hud.maxExp;
	save.data.level = hud.level;
	save.flush();
	FlxG.switchState(new MenuState());
}
</code></pre>
<p>We create a new FlxSave object and bind it to a save file using our static keyword. Before saving any data, we'll erase all the existing data under that save file.</p>
<p>Once the data is erased, we bind() the FlxSave instance using the keyword again and then, using its "data" property, save all the HUD properties that we'll later load.</p>
<p>When the game is saved, we return the player to the main menu.</p>
<p>Since we're accessing these HUD properties, we'll need to make them public in HUD.hx:</p>
<pre><code class="haxe">public var hp:Int;
public var maxHp:Int;
public var exp:Int;
public var maxExp:Int;
public var level:Int;
</code></pre>
<p>Back to PlayState.hx, add the loadGame() method, which loads the data instead of saving it:</p>
<pre><code class="haxe">public function loadGame():Void {
	var save:FlxSave = new FlxSave();
	save.bind(SAVE_NAME);
	hud.hp = save.data.hp;
	hud.maxHp = save.data.maxHp;
	hud.exp = save.data.exp;
	hud.maxExp = save.data.maxExp;
	hud.level = save.data.level;
}
</code></pre>
<p>We can now save our player data, let's add the ability to load it.</p>
<p>Update the MenuState.hx code:</p>
<pre><code class="haxe">package ;

import flixel.FlxG;
import flixel.FlxSprite;
import flixel.FlxState;
import flixel.text.FlxText;
import flixel.ui.FlxButton;
import flixel.util.FlxMath;
import flixel.util.FlxSave;

/**
 * A FlxState which can be used for the game's menu.
 */
class MenuState extends FlxState
{
	/**
	 * Function that is called up when to state is created to set it up.
	 */
	override public function create():Void
	{
		super.create();
		
		var init_x:Int = Math.floor(FlxG.width / 2 - 40);
		
		var btn_new = new FlxButton(init_x, 50, &quot;New game&quot;, onNew);
		add(btn_new);
		
		var save:FlxSave = new FlxSave();
		save.bind(PlayState.SAVE_NAME);
		
		if (save.data.hp!=null) {
			var btn_load = new FlxButton(init_x, 80, &quot;Load&quot;, onLoad);
			add(btn_load);
		}
	}
	
	private function onNew():Void {
		var playState:PlayState = new PlayState();
		FlxG.switchState(playState);
		playState.loadedGame = false;
	}
	
	private function onLoad():Void {
		var playState:PlayState = new PlayState();
		FlxG.switchState(playState);
		playState.loadedGame = true;
	}

	/**
	 * Function that is called when this state is destroyed - you might want to
	 * consider setting all objects this state uses to null to help garbage collection.
	 */
	override public function destroy():Void
	{
		super.destroy();
	}

	/**
	 * Function that is called once every frame.
	 */
	override public function update():Void
	{
		super.update();
	}
}</code></pre>
<p>First of all, we only display the "Load game" button if a save file exists. We can check this by comparing any of the saved properties to null.</p>
<p>Then we simply set the PlayState instance's loadedGame property accordingly in the button handler functions.</p>
<p>The PlayState.hx code ends up looking like this:</p>
<pre><code class="haxe">package ;

import flixel.FlxCamera;
import flixel.FlxG;
import flixel.FlxObject;
import flixel.FlxSprite;
import flixel.FlxState;
import flixel.group.FlxTypedGroup;
import flixel.system.FlxSound;
import flixel.tile.FlxTilemap;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.ui.FlxButton;
import flixel.util.FlxColor;
import flixel.util.FlxPath;
import flixel.util.FlxPoint;
import flixel.util.FlxSave;
import openfl.Assets;

enum PlayerAction {
	Walking;
	Combat;
}

/**
 * A FlxState which can be used for the actual gameplay.
 */
class PlayState extends FlxState
{
	private var tileMap:FlxTilemap;
	public static var TILE_WIDTH:Int = 16;
	public static var TILE_HEIGHT:Int = 16;
	public static var LEVEL_WIDTH:Int = 50;
	public static var LEVEL_HEIGHT:Int = 50;
	public static var CAMERA_SPEED:Int = 8;
	private var camera:FlxCamera;
	private var cameraFocus:FlxSprite;
	private var movementMarker:FlxSprite;
	public var hero:FlxSprite;
	private var path:FlxPath;
	public var hud:HUD;
	private var potions:FlxTypedGroup&lt;Potion&gt;;
	private var enemies:Array&lt;Enemy&gt;;
	private var currentAction:PlayerAction;
	private var combatWindow:CombatWindow;
	private var combatHide:FlxTween;
	
	private var saveButton:FlxButton;
	public static var SAVE_NAME:String = &quot;RPG_Save&quot;;
	public var loadedGame:Bool;
	
	/**
	 * Function that is called up when to state is created to set it up.
	 */
	override public function create():Void
	{
		super.create();
		
		FlxG.worldBounds.width = TILE_WIDTH * LEVEL_WIDTH;
		FlxG.worldBounds.height = TILE_HEIGHT * LEVEL_HEIGHT;
		
		tileMap = new FlxTilemap();
		tileMap.loadMap(Assets.getText(&quot;assets/data/map.csv&quot;), &quot;assets/images/tileset.png&quot;, TILE_WIDTH, TILE_HEIGHT, 0, 1);
		tileMap.setTileProperties(0, FlxObject.ANY);
		tileMap.setTileProperties(1, FlxObject.ANY);
		tileMap.setTileProperties(2, FlxObject.NONE);
		add(tileMap);
		
		cameraFocus = new FlxSprite();
		cameraFocus.makeGraphic(1, 1, FlxColor.TRANSPARENT);
		add(cameraFocus);
		
		camera = FlxG.camera;
		camera.follow(cameraFocus, FlxCamera.STYLE_LOCKON);
		
		movementMarker = new FlxSprite();
		movementMarker.visible = false;
		add(movementMarker);
		
		hero = new FlxSprite(TILE_WIDTH * 7, TILE_HEIGHT * 3);
		hero.loadGraphic(&quot;assets/images/hero.png&quot;, true, TILE_WIDTH, TILE_HEIGHT);
		hero.animation.add(&quot;down&quot;, [0, 1, 0, 2]);
		hero.animation.add(&quot;up&quot;, [3, 4, 3, 5]);
		hero.animation.add(&quot;right&quot;, [6, 7, 6, 8]);
		hero.animation.add(&quot;left&quot;, [9, 10, 9, 11]);
		add(hero);
		
		potions = new FlxTypedGroup&lt;Potion&gt;();
		add(potions);
		spawnPotion(5, 5);
		spawnPotion(6, 5);
		spawnPotion(3, 10);
		spawnPotion(4, 10);
		spawnPotion(1, 10);
		
		hero.animation.play(&quot;down&quot;);
		path = new FlxPath();
		
		enemies = new Array&lt;Enemy&gt;();
		addEnemy(10, 15);
		addEnemy(12, 10);
		addEnemy(15, 6);
		addEnemy(20, 6);
		addEnemy(12, 20);
		
		hud = new HUD();
		add(hud);
		
		currentAction = Walking;
		hero.active = true;
		
		combatWindow = new CombatWindow(this);
		combatWindow.active = false;
		combatWindow.visible = false;
		add(combatWindow);
		
		saveButton = new FlxButton(FlxG.width - 80, 0, &quot;Save&quot;, doSave);
		add(saveButton);
		
		if (loadedGame) {
			loadGame();
		}
	}
	
	public function loadGame():Void {
		var save:FlxSave = new FlxSave();
		save.bind(SAVE_NAME);
		hud.hp = save.data.hp;
		hud.maxHp = save.data.maxHp;
		hud.exp = save.data.exp;
		hud.maxExp = save.data.maxExp;
		hud.level = save.data.level;
	}
	
	private function doSave():Void {
		var save:FlxSave = new FlxSave();
		// Delete all existing data
		save.bind(SAVE_NAME);
		save.erase();
		// Write new data
		save.bind(SAVE_NAME);
		save.data.hp = hud.hp;
		save.data.maxHp = hud.maxHp;
		save.data.exp = hud.exp;
		save.data.maxExp = hud.maxExp;
		save.data.level = hud.level;
		save.flush();
		FlxG.switchState(new MenuState());
	}
	
	private function spawnPotion(x:Int, y:Int):Void{
		var potion:Potion = new Potion();
		potion.x = x * TILE_WIDTH;
		potion.y = y * TILE_HEIGHT;
		potions.add(potion);
	}
	
	private function addEnemy(x:Int, y:Int):Void {
		var enemy:Enemy = new Enemy(tileMap, hero);
		enemy.x = x * TILE_WIDTH;
		enemy.y = y * TILE_HEIGHT;
		enemy.health = 5;
		enemies.push(enemy);
		add(enemy);
	}
	
	private function onPotionCollision(hero:FlxSprite, potion:Potion):Void {
		if (potion.exists &amp;&amp; hero.exists) {
			potion.kill();
			hud.addHealth(1);
		}
	}
	
	private function onEnemyCollision(hero:FlxSprite, enemy:Enemy):Void {
		if (enemy.exists &amp;&amp; hero.exists &amp;&amp; hero.active &amp;&amp; enemy.active) {
			hero.active = false;
			enemy.active = false;
			currentAction = Combat;
			startCombat(enemy);
		}
	}
	
	private function startCombat(enemy:Enemy):Void {
		combatWindow.active = true;
		combatWindow.visible = true;
		if (combatHide!=null &amp;&amp; combatHide.active) {
			combatHide.cancel();
		}
		FlxTween.tween(combatWindow, { y: FlxG.height / 2 - 40 }, 1, { type: FlxTween.ONESHOT, ease: FlxEase.quadOut } );
		combatWindow.y = -200;
		combatWindow.fight(enemy);
	}
	
	public function winCombat(enemy:Enemy):Void {
		endCombat(enemy);
	}
	
	public function endCombat(enemy:Enemy):Void {
		enemy.kill();
		combatHide = FlxTween.tween(combatWindow, {y: -200 }, 1, { type: FlxTween.ONESHOT, ease: FlxEase.quadIn, complete: hideCombat} );
		hero.active = true;
		currentAction = Walking;
	}
	
	private function hideCombat(tween:FlxTween):Void {
		combatWindow.active = false;
		combatWindow.visible = false;	
	}

	/**
	 * Function that is called when this state is destroyed - you might want to
	 * consider setting all objects this state uses to null to help garbage collection.
	 */
	override public function destroy():Void
	{
		super.destroy();
	}

	/**
	 * Function that is called once every frame.
	 */
	override public function update():Void
	{
		super.update();
		
		// Save visibility
		saveButton.visible = currentAction == Walking;
		
		// Collisions
		FlxG.overlap(hero, potions, onPotionCollision);
		var i:Int;
		for (i in 0...enemies.length) {
			FlxG.overlap(hero, enemies[i], onEnemyCollision);
		}
		
		// Animation
		if (!path.finished &amp;&amp; path.nodes!=null) {
			if (path.angle == 0 || path.angle == 45 || path.angle == -45) {
				hero.animation.play(&quot;up&quot;);
			}
			if (path.angle == 180 || path.angle == -135 || path.angle == 135) {
				hero.animation.play(&quot;down&quot;);
			}
			if (path.angle == 90) {
				hero.animation.play(&quot;right&quot;);
			}
			if (path.angle == -90) {
				hero.animation.play(&quot;left&quot;);
			}
		} else {
			hero.animation.curAnim.curFrame = 0;
			hero.animation.curAnim.stop();
		}
		
		// Camera movement
		if (FlxG.keys.anyPressed([&quot;DOWN&quot;, &quot;S&quot;])) {
			cameraFocus.y += CAMERA_SPEED;
		}
		if (FlxG.keys.anyPressed([&quot;UP&quot;, &quot;W&quot;])) {
			cameraFocus.y -= CAMERA_SPEED;
		}
		if (FlxG.keys.anyPressed([&quot;RIGHT&quot;, &quot;D&quot;])) {
			cameraFocus.x += CAMERA_SPEED;
		}
		if (FlxG.keys.anyPressed([&quot;LEFT&quot;, &quot;A&quot;])) {
			cameraFocus.x -= CAMERA_SPEED;
		}
		
		// Camera bounds
		if (cameraFocus.x &lt; FlxG.width / 2) {
			cameraFocus.x = FlxG.width / 2;
		}
		if (cameraFocus.x &gt; LEVEL_WIDTH * TILE_WIDTH - FlxG.width / 2) {
			cameraFocus.x = LEVEL_WIDTH * TILE_WIDTH - FlxG.width / 2;
		}
		if (cameraFocus.y &lt; FlxG.height / 2) {
			cameraFocus.y = FlxG.height / 2;
		}
		if (cameraFocus.y &gt; LEVEL_HEIGHT * TILE_HEIGHT - FlxG.height / 2) {
			cameraFocus.y = LEVEL_HEIGHT * TILE_HEIGHT - FlxG.height / 2;
		}
		
		// Mouse clicks
		if (currentAction == Walking &amp;&amp; FlxG.mouse.justReleased){
			var tileCoordX:Int = Math.floor(FlxG.mouse.x / TILE_WIDTH);
			var tileCoordY:Int = Math.floor(FlxG.mouse.y / TILE_HEIGHT);
			
			movementMarker.visible = true;
			if (tileMap.getTile(tileCoordX, tileCoordY) == 2) {
				var nodes:Array&lt;FlxPoint&gt; = tileMap.findPath(FlxPoint.get(hero.x + TILE_WIDTH/2, hero.y + TILE_HEIGHT/2), FlxPoint.get(tileCoordX * TILE_WIDTH + TILE_WIDTH/2, tileCoordY * TILE_HEIGHT + TILE_HEIGHT/2));
				if (nodes != null) {
					path.start(hero, nodes);
					movementMarker.loadGraphic(AssetPaths.marker_move__png, false, TILE_WIDTH, TILE_HEIGHT);
				}else {
					movementMarker.loadGraphic(AssetPaths.marker_stop__png, false, TILE_WIDTH, TILE_HEIGHT);
				}
			}else {
				movementMarker.loadGraphic(AssetPaths.marker_stop__png, false, TILE_WIDTH, TILE_HEIGHT);
			}
			movementMarker.setPosition(tileCoordX * TILE_WIDTH, tileCoordY * TILE_HEIGHT);
		}
	}
}</code></pre>
<p>And now we can save player data, such as health, level and experience.</p>
<p>This may be enough for some games, but next time we'll expand this system, <a href="55_haxeflixel_rpg_tutorial_part_18.html">adding the ability to save and load additional data, such as player's position, remaining enemies and potions</a>.</p><footer>&copy; Kirill Poletaev</footer></body></html>