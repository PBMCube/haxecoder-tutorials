<html><head><style>body{font-family:Arial, Helvetica, sans-serif; padding:20px;}pre{ background:#eee; padding:20px; }footer{color:#666; border-top:1px solid #666; margin-top:20px; padding-top:20px; width:100%;}</style><title>51. HaxeFlixel RPG tutorial: Part 14</title></head><body><h1>51. HaxeFlixel RPG tutorial: Part 14</h1><h3>2014-10-22</h3><img src="img/104.png" class="thumb" alt="HaxeFlixel RPG tutorial leveling">
<p>Today we'll improve our existing combat system, by adding experience rewards, leveling up, leveled player damage, random enemy damage, and the ability to level up.</p>
<p>Currently, all of our player stat data, such as health and experience, is stored and displayed using the HUD.hx class.</p>
<p>We will add new methods to the class, which will let us manipulate the player's experience and automatically level the player up.</p>
<p>It's also required to add a conditional statement, which responds to the player's health becoming 0 or negative.</p><p>Go to the HUD.hx class now and add an addExp() function, which increments a value to the experience bar and handles leveling up:</p>
<pre><code class="haxe">public function addExp(num:Int):Void {
	exp += num;
	while (exp &gt; maxExp) {
		level++;
		exp -= maxExp;
		maxExp = Math.ceil(maxExp * 1.3);
		hp++;
		maxHp++;
	}
}</code></pre>
<p>You can see that by gaining a level the player's max experience value is multiplied by 1.3, and their health and maximum health is increased by 1.</p>
<p>Add an additional if statement to addHealth(), which switches the state to MenuState whenever the health drops to 0 or below.</p>
<pre><code class="haxe">public function addHealth(num:Int):Void {
	hp += num;
	if (hp &gt; maxHp) {
		hp = maxHp;
	}
	if (hp &lt;= 0) {
		FlxG.switchState(new MenuState());
	}
}
</code></pre>
<p>Add a method to read the level value:</p>
<pre><code class="haxe">public function getLevel():Int {
	return level;
}
</code></pre>
<p>Overall, the HUD.hx class now looks like this:</p>
<pre><code class="haxe">package ;
import flixel.FlxG;
import flixel.group.FlxSpriteGroup;
import flixel.text.FlxText;
import flixel.ui.FlxBar;

/**
 * RPG HUD.
 * @author Kirill Poletaev
 */
class HUD extends FlxSpriteGroup
{
	private var healthDisplay:FlxText;
	private var levelDisplay:FlxText;
	private var expBar:FlxBar;
	
	private var hp:Int;
	private var maxHp:Int;
	private var exp:Int;
	private var maxExp:Int;
	private var level:Int;
	
	public function new() 
	{
		super();
		scrollFactor.x = 0;
		scrollFactor.y = 0;
		
		healthDisplay = new FlxText(2, 2);
		hp = 5;
		maxHp = 10;
		add(healthDisplay);
		
		levelDisplay = new FlxText(2, 12);
		level = 1;
		add(levelDisplay);
		
		maxExp = 10;
		exp = 0;
		expBar = new FlxBar(4, 25, FlxBar.FILL_LEFT_TO_RIGHT, 100, 4);
		expBar.createFilledBar(0xFF63460C, 0xFFE6AA2F);
		add(expBar);
	}
	
	override public function update() {
		healthDisplay.text = &quot;Health: &quot; + hp + &quot;/&quot; + maxHp;
		levelDisplay.text = &quot;Level: &quot; + level;
		expBar.currentValue = exp;
		expBar.setRange(0, maxExp);
	}
	
	public function addHealth(num:Int):Void {
		hp += num;
		if (hp &gt; maxHp) {
			hp = maxHp;
		}
		if (hp &lt;= 0) {
			FlxG.switchState(new MenuState());
		}
	}
	
	public function addExp(num:Int):Void {
		exp += num;
		while (exp &gt; maxExp) {
			level++;
			exp -= maxExp;
			maxExp = Math.ceil(maxExp * 1.3);
			hp++;
			maxHp++;
		}
	}
	
	public function getLevel():Int {
		return level;
	}
	
}</code></pre>
<p>Now go to CombatWindow.hx and update the onAttack() method to use the available HUD methods for proper damage and health calculations:</p>
<pre><code class="haxe">private function onAttack() {
	var dmg:Int = playState.hud.getLevel();
	enemy.health -= dmg;
	txt.text = &quot;You hit the enemy, dealing &quot; + dmg + &quot; damage.&quot;;
	if (enemy.health &gt; 0) {
		var enemyDmg:Int = Math.floor(Math.random()*2);
		txt.text += &quot;\nThe enemy strikes, dealing &quot; + enemyDmg + &quot; damage.&quot;;
		playState.hud.addHealth( -enemyDmg);
	} else {
		playState.winCombat(enemy);
		playState.hud.addExp(6);
	}
}
</code></pre>
<p>You can see that I made the player's damage fully depend on the player's current level. The damage dealt by enemy is randomized, in this case between the values 0 and 1. Six experience points are added to the player whenever they win a battle.</p>
<p>Back in PlayState.hx, it is necessary to make the HUD instance public and accessible inside of the CombatWindow object:</p>
<pre><code class="haxe">public var hud:HUD;</code></pre>
<p>Done, now you should be able to gain experience by killing enemies and eventually level up. If you lose a battle, you're sent back to the menu state.</p>
<p>We'll <a href="52_haxeflixel_rpg_tutorial_part_15.html">add tween animations to our combat window next time</a>!</p><footer>&copy; Kirill Poletaev</footer></body></html>